plugins {
	alias(libs.plugins.fabric.loom)
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	mavenCentral()
	maven {
		// Location of the maven that hosts Team Resourceful's jars.
		name = "Team Resourceful Maven"
		url = "https://maven.teamresourceful.com/repository/maven-public/"
	}
}

dependencies {
	// Base dependencies
	minecraft(libs.minecraft)
	mappings loom.officialMojangMappings()
	modImplementation(libs.fabric.loader)
	modImplementation(libs.fabric.api)

	// Additional dependencies
	modImplementation(libs.resourceful.config)
	modImplementation(libs.resourceful.lib)
	modImplementation(libs.resourceful.cosmetics)
	modImplementation(libs.geckolib)
	modImplementation(libs.bytecodecs)

	// Shadow dependencies
	include(libs.resourceful.cosmetics)
}

fabricApi {
	configureDataGeneration() {
		client = true
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}
